subprojects {

    apply plugin: "java"

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()

    jar {
        manifest {
            attributes([
                    "Implementation-Version": project.jar.archiveVersion
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = "Sponge"
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 17
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

task makeAllDataPacks(type: GradleBuild) {
    tasks = ["makeErrorsAsDatapack", "makeFeatureCycleAsDataPack"]
}

task makeErrorsAsDatapack(type: Exec) {
    workingDir("${projectDir}/tests/errors_as_datapack")
    commandLine("jar", "-cMf", "../errors_as_datapack.zip", "pack.mcmeta", "data")
}

task makeFeatureCycleAsDataPack(type: Exec) {
    workingDir("${projectDir}/tests/feature_cycle_as_datapack")
    commandLine("jar", "-cMf", "../feature_cycle_as_datapack.zip", "pack.mcmeta", "data")
}